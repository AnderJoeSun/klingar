/*
 * Copyright (C) 2016 Simon Norberg
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'checkstyle'

task checkstyle(type: Checkstyle) {
  group 'verification'
  configFile rootProject.file('checkstyle.xml')
  source 'src/main/java'
  ignoreFailures false
  showViolations true
  include '**/*.java'
  exclude '**/gen/**'

  classpath = files()
}

afterEvaluate {
  if (project.tasks.findByName('check')) {
    check.dependsOn('checkstyle')
  }
}

def versionMajor = 0
def versionMinor = 7
def versionPatch = 0

def gitSha() {
  def p = 'git rev-parse --short HEAD'.execute([], project.rootDir)
  p.waitFor()
  if (p.exitValue() != 0) {
    throw new RuntimeException(p.errorStream.text)
  }

  return p.text.trim()
}

def gitTimestamp() {
  def p = 'git log -n 1 --format=%at'.execute([], rootDir)
  p.waitFor()
  if (p.exitValue() != 0) {
    throw new RuntimeException(p.errorStream.text)
  }

  return p.text.trim()
}

def isTravis = "true" == System.getenv("TRAVIS")
def preDexEnabled = "true" == System.getProperty("pre-dex", "true")

android {
  compileSdkVersion 25
  buildToolsVersion '25.0.2'

  dexOptions {
    preDexLibraries = preDexEnabled && !isTravis
  }
  defaultConfig {
    applicationId "net.simno.klingar"
    testApplicationId "net.simno.klingar.test"
    minSdkVersion 21
    targetSdkVersion 25

    versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
    versionName "${versionMajor}.${versionMinor}.${versionPatch}"

    buildConfigField 'String', 'GIT_SHA', "\"${gitSha()}\""
    buildConfigField 'long', 'GIT_TIMESTAMP', "${gitTimestamp()}L"
    buildConfigField "String", "CAST_APP_ID", "\"631E34EA\""
    buildConfigField 'String', 'DEBUG_USER', "\"${project.findProperty('debugUser') ?: 'null'}\""
    buildConfigField 'String', 'DEBUG_PWD', "\"${project.findProperty('debugPwd') ?: 'null'}\""

    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  lintOptions {
    abortOnError false
  }
  buildTypes {
    debug {
      debuggable true
      minifyEnabled false
    }
    release {
      debuggable false
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
  packagingOptions {
    exclude 'LICENSE.txt'
  }
  testOptions {
    unitTests.returnDefaultValues = true
  }
}

ext {
  autoParcelVersion = '0.2.5'
  autoValueVersion = '1.4.1'
  butterknifeVersion = '8.5.1'
  conductorVersion = '2.1.2'
  daggerVersion = '2.10'
  leakCanaryVersion = '1.5'
  okhttpVersion = '3.7.0'
  retrofitVersion = '2.2.0'
  rxlifecycleVersion = '2.0.1'
  supportLibVersion = '25.3.1'
}

dependencies {
  compile "com.android.support:appcompat-v7:${supportLibVersion}"
  compile "com.android.support:mediarouter-v7:${supportLibVersion}"
  compile "com.android.support:recyclerview-v7:${supportLibVersion}"
  compile "com.android.support:design:${supportLibVersion}"

  compile "com.bluelinelabs:conductor:${conductorVersion}"
  compile "com.bluelinelabs:conductor-rxlifecycle2:${conductorVersion}"

  compile 'com.github.bumptech.glide:glide:3.7.0'
  compile 'com.github.bumptech.glide:okhttp-integration:1.4.0'

  compile 'com.google.android.exoplayer:exoplayer:r2.3.1'
  compile 'com.google.android.gms:play-services-cast-framework:10.2.1'

  provided "com.google.auto.value:auto-value:${autoValueVersion}"
  annotationProcessor "com.google.auto.value:auto-value:${autoValueVersion}"
  testAnnotationProcessor "com.google.auto.value:auto-value:${autoValueVersion}"
  annotationProcessor 'com.ryanharter.auto.value:auto-value-moshi:0.4.3'
  annotationProcessor "com.ryanharter.auto.value:auto-value-parcel:${autoParcelVersion}"
  compile "com.ryanharter.auto.value:auto-value-parcel-adapter:${autoParcelVersion}"

  compile "com.google.dagger:dagger:${daggerVersion}"
  annotationProcessor "com.google.dagger:dagger-compiler:${daggerVersion}"

  compile "com.jakewharton:butterknife:${butterknifeVersion}"
  annotationProcessor "com.jakewharton:butterknife-compiler:${butterknifeVersion}"
  compile 'com.jakewharton.timber:timber:4.5.1'
  compile 'com.jakewharton.rxrelay2:rxrelay:2.0.0'

  debugCompile "com.squareup.leakcanary:leakcanary-android:${leakCanaryVersion}"
  releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:${leakCanaryVersion}"
  testCompile "com.squareup.leakcanary:leakcanary-android-no-op:${leakCanaryVersion}"
  compile 'com.squareup.moshi:moshi:1.4.0'
  compile "com.squareup.okhttp3:okhttp:${okhttpVersion}"
  compile "com.squareup.okhttp3:logging-interceptor:${okhttpVersion}"
  compile "com.squareup.retrofit2:retrofit:${retrofitVersion}"
  compile "com.squareup.retrofit2:adapter-rxjava2:${retrofitVersion}"
  compile("com.squareup.retrofit2:converter-simplexml:${retrofitVersion}") {
    exclude group: 'stax', module: 'stax-api'
    exclude group: 'stax', module: 'stax'
    exclude group: 'xpp3', module: 'xpp3'
  }

  compile "com.trello.rxlifecycle2:rxlifecycle:${rxlifecycleVersion}"
  compile "com.trello.rxlifecycle2:rxlifecycle-android:${rxlifecycleVersion}"

  compile 'de.psdev.licensesdialog:licensesdialog:1.8.2'

  compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
  compile 'io.reactivex.rxjava2:rxjava:2.0.8'

  provided 'javax.annotation:jsr250-api:1.0'

  testCompile 'junit:junit:4.12'
  testCompile 'org.hamcrest:hamcrest-library:1.3'
  testCompile 'org.mockito:mockito-core:2.7.22'

  androidTestCompile "com.android.support:support-annotations:${supportLibVersion}"
  androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
  androidTestCompile 'com.android.support.test:runner:0.5'
  androidTestCompile 'com.android.support.test:rules:0.5'
  androidTestCompile 'com.google.code.findbugs:jsr305:3.0.1'
}
