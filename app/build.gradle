/*
 * Copyright (C) 2015 Simon Norberg
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'me.tatarka.retrolambda'

def versionMajor = 0
def versionMinor = 1
def versionPatch = 0
def versionBuild = 0

def gitSha() {
  def p = 'git rev-parse --short HEAD'.execute([], project.rootDir)
  p.waitFor()
  if (p.exitValue() != 0) {
    throw new RuntimeException(p.errorStream.text)
  }

  return p.text.trim()
}

def gitTimestamp() {
  def p = 'git log -n 1 --format=%at'.execute([], rootDir)
  p.waitFor()
  if (p.exitValue() != 0) {
    throw new RuntimeException(p.errorStream.text)
  }

  return p.text.trim()
}

def isTravis = "true".equals(System.getenv("TRAVIS"))
def preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))

android {
  compileSdkVersion 24
  buildToolsVersion '24.0.0'

  dexOptions {
    preDexLibraries = preDexEnabled && !isTravis
  }

  defaultConfig {
    applicationId "net.simno.klingar"
    minSdkVersion 21
    targetSdkVersion 24

    versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
    versionName "${versionMajor}.${versionMinor}.${versionPatch}"

    buildConfigField 'String', 'GIT_SHA', "\"${gitSha()}\""
    buildConfigField 'long', 'GIT_TIMESTAMP', "${gitTimestamp()}L"
    buildConfigField "String", "CAST_APP_ID", "\"01C414FF\""

    testInstrumentationRunner "net.simno.klingar.MockTestRunner"
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  lintOptions {
    abortOnError false
  }
  buildTypes {
    debug {
      debuggable true
      minifyEnabled false
      applicationIdSuffix '.debug'
    }
    release {
      debuggable false
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
  packagingOptions {
    exclude 'LICENSE.txt'
  }
}

ext {
  supportLibVersion = '24.0.0'
  daggerVersion = '2.5'
  okhttpVersion = '3.3.1'
  retrofitVersion = '2.1.0'
  mockitoVersion = '1.10.19'
}

dependencies {
  compile "com.android.support:appcompat-v7:${supportLibVersion}"
  compile "com.android.support:cardview-v7:${supportLibVersion}"
  compile "com.android.support:design:${supportLibVersion}"
  compile "com.android.support:mediarouter-v7:${supportLibVersion}"
  compile "com.android.support:recyclerview-v7:${supportLibVersion}"
  compile "com.android.support:support-v13:${supportLibVersion}"

  compile 'com.facebook.conceal:conceal:1.1.2@aar'

  compile 'com.github.bumptech.glide:glide:3.7.0'
  compile 'com.github.bumptech.glide:okhttp-integration:1.4.0'

  compile 'com.google.android.gms:play-services-cast:9.0.2'
  compile 'com.google.android.libraries.cast.companionlibrary:ccl:2.8.4'
  compile "com.google.dagger:dagger:${daggerVersion}"
  apt "com.google.dagger:dagger-compiler:${daggerVersion}"

  compile 'com.jakewharton:butterknife:8.1.0'
  apt 'com.jakewharton:butterknife-compiler:8.1.0'
  compile 'com.jakewharton.timber:timber:4.1.2'

  compile "com.squareup.okhttp3:okhttp:${okhttpVersion}"
  compile "com.squareup.okhttp3:logging-interceptor:${okhttpVersion}"
  compile "com.squareup.retrofit2:retrofit:${retrofitVersion}"
  compile "com.squareup.retrofit2:adapter-rxjava:${retrofitVersion}"
  compile("com.squareup.retrofit2:converter-simplexml:${retrofitVersion}") {
    exclude group: 'stax', module: 'stax-api'
    exclude group: 'stax', module: 'stax'
    exclude group: 'xpp3', module: 'xpp3'
  }

  compile 'com.trello:rxlifecycle-components:0.6.1'

  compile 'io.reactivex:rxandroid:1.2.1'
  compile 'io.reactivex:rxjava:1.1.6'

  provided 'javax.annotation:jsr250-api:1.0'

  androidTestCompile "com.android.support:support-annotations:${supportLibVersion}"
  androidTestCompile 'com.android.support.test:rules:0.5'
  androidTestCompile 'com.android.support.test:runner:0.5'
  androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'

  androidTestApt "com.google.dagger:dagger-compiler:${daggerVersion}"
  androidTestCompile 'com.google.dexmaker:dexmaker:1.2'
  androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.2'

  androidTestCompile "com.squareup.okhttp3:mockwebserver:${okhttpVersion}"
  androidTestCompile "com.squareup.retrofit2:retrofit-mock:${retrofitVersion}"

  testCompile 'junit:junit:4.12'

  testCompile "org.mockito:mockito-core:${mockitoVersion}"
  androidTestCompile "org.mockito:mockito-core:${mockitoVersion}"
}
